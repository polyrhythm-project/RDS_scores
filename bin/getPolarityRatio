#!/usr/bin/perl
#
# Programmer:    Craig Stuart Sapp <craig.stanford.edu>
# Creation Date: Sat Jun 19 20:27:11 PDT 2021
# Last Modified: Sat Jun 19 20:27:13 PDT 2021
# Filename:      getPoliarityRatio
# Syntax:        perl 5
#
# Description:   Calculate Polarity ratio:
# 
#                abs [(group-A-coincide-notes divided by group-A-total-notes) - (group-B-coincide-notes divided by group-B-total-notes) ] / composite-total-notes
#
#                INTERPRETATION: higher = more polarity; lower = more balanced/less polarity
#                NOTE: In theory, polarity (i.e., salience of one group in comparison
#                to the other) has a direction toward one or the other group; here,
#                absolute values are used because grp A vs. grp B is arbitrary except
#                for the fact that A will always include the lowest part.
#
#                Polarity may interact with nestedness, but I am not sure  if it
#                relates directly to complexity. It is more about whether one stream
#                will be heard as more salient, and to what degree.
#

use strict;
use Getopt::Long;

my $composite = `which composite`;
chomp $composite;
die "Cannot find composite tool.  Install humlib (https://github.com/craigsapp/humlib)\n" if $composite =~ /^\s*$/;

my $verboseQ = 0;
my $fileQ = 0;
my $outputQ  = 0;
Getopt::Long::Configure("bundling");
GetOptions (
   'v|verbose' => \$verboseQ,
   'f|file' => \$fileQ,
   'o|output' => \$outputQ
);

my @files = @ARGV;

die "Type 'make experiment' first." if @files == 0;

for my $file (@files) {
	process($file);
}


##############################
##
## process -- extract data and calculate score
## 
##   !!!group-b-event-count: 31         # number of events in group B
##   !!!group-a-event-count: 28         # number of events in group A
##   !!!composite-event-count: 47       # number of events in full composite rhythm
##   !!!coincidence-event-count: 14     # number of events in concidence 
##

sub process {
	my ($file) = @_;
	my @contents = `cat $file | $composite -eg | $composite -eg`;
	if ($outputQ) {
		print "\nOUTPUT DATA:\n";
		print "====================================\n";
		print @contents;
		print "====================================\n\n";
	}
	chomp @contents;
	my $groupAtotal    = -1;
	my $groupBtotal    = -1;
	my $compositeTotal = -1;
	foreach my $line (@contents) {
		if ($line =~ /^!!!group-a-event-count:\s*(\d+)/) {
			$groupAtotal = $1;
		} elsif ($line =~ /^!!!group-b-event-count:\s*(\d+)/) {
			$groupBtotal = $1;
		} elsif ($line =~ /^!!!composite-event-count:\s*(\d+)/) {
			$compositeTotal = $1;
		}
	}

	if ($groupAtotal <= 0) {
		print "ERROR NO !!!group-a-event-count: reference record for $file\n";
	} elsif ($groupBtotal <= 0) {
		print "ERROR NO !!!group-b-event-count: reference record for $file\n";
	} elsif ($compositeTotal <= 0) {
		print "ERROR NO !!!composite-event-count: reference record for $file\n";
	} else {
		# old calculation: my $value = abs($groupAcoincide / $groupAtotal - $groupBcoincide / $groupBtotal) / $compositeTotal;
		my $value = abs($groupAtotal - $groupBtotal) * 1.0 / $compositeTotal;
		$value = int($value * 1000.0 + 0.5) / 1000.0;
		if ($fileQ) {
			print "$value\t$file\n";
		} else {
			print "$value\n";
		}
		if ($verboseQ) {
			print "\tcomposite notes: $compositeTotal\n";
			print "\tgroup A notes: $groupAtotal\n";
			print "\tgroup B notes: $groupBtotal\n";
		}
	}
}



